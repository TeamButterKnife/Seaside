title: start
---
Convenient_Narrator: <i>It's a lovely day here in the small town of Peach Beach. Alas, working weekends at the peach farm isn't exciting at all.</i>
Convenient_Narrator: <i>Piper is ready to leave to carry on the family legacy and become a pirate.</i>
Convenient_Narrator: <i>Perhaps Uncle Al would have some advice.</i>
===
title: preShipAl
---
<<if $ship == true>>
<<jump al>>
<<endif>>
Uncle_Al: There you are.
    -> Uncle Al, It's time for me to get into piracy.
        Uncle_Al: What? You're too young to be a pirate. You don't even have a ship.
        Uncle_Al: Shame about losing mine to the royal navy...
===
title: preShipBrawk
---
<<if $ship == true>>
<<jump brawk>>
<<endif>>
Brawk: Buzz off.
    -> I was just saying hi.
        Brawk: Too bad.
            Convenient_Narrator: <i>Piper learns that Brawk is a certain sort of fellow with a certain sort of attitude.</i>
                <<die>>
===
title: preShipPeach
---
<<if $ship == true >>
<<jump peach>>
<<endif>>
Peach: I think that kid stole some of my peaches...
    -> Hey, Peach. Guess what?
        Peach: Let me guess. You've grown up enough that it's pirate time? A lot goes into that, Piper.
===
title: preShipKG
---
<<if $ship == true>>
<<jump KG>>
<<endif>>
King_Gull: What's up, kid?
    -> I'm finally going to go pirating. You used to be a sailor. If anyone understands, it's you.
        King_Gull: I guess, but neither ship nor crew? Are you really ready for he pirate life?
===
title: ship
---
Convenient_Narrator: <i>Down by the water, Piper finds a busted up old ship with the faded name “Cannonball Z” on the hull..</i>
	->It's not that busted up.
		Convenient_Narrator: <i>With a massive chunk missing and no mast to speak of, fixing it will be quite a task, but if she's up to the challenge…</i>
			-> I think I might have a ship.
            << set $ship=true>>
===
title: peach
---
<<if $fetchQuest == true>>
<<jump getRum>>
<<endif>>
Peach: Amazing how the flinging of sand all over my fruit stops when other folks come over.
    -> Sorry about the sand, but I have good news. I found a ship!
        Peach: Wow. It usually takes much longer for that part. Anyway...
        Peach: I know I'm bragging but we have the best peaches on the coast.
            -> Really?
                Peach: Oh, don't tease. You know I'm right. They're a wonderful addition to beverages!
===
title: al
---
<<if $fetchQuest == true>>
<<jump getHat>>
<<endif>>
Uncle_Al: Took a walk, huh?
    -> You're not gonna believe this, but I found a ship washed up on the beach called the Cannonball Z.
        Uncle_Al: That's my ship! Well, tell ya what. Maybe you can have her if you find a mentor.
        Uncle_Al: Other than stories, you don't know squat about pirating, and I'm too me to teach ya.
===
title: KG
---
<<if $hat == true>>
<<jump onlyHat>>
<<endif>>
<<if $rum == true>>
<<jump onlyRum>>
<<endif>>
<<if $hat == true && $rum == true>>
<<jump gotBoth>>
<<endif>>
King_Gull: Back again?
    -> Guess who found a ship? It needs a bit of work, but it's a boat.
        King_Gull: Found a ship, huh? Well, a newbie pirate still needs a mentor.
            -> Could you be my mentor?
                King_Gull: Ha, not that easily, kid. I like tradition, so I need three things to know you're serious:
                King_Gull: A good hat, a bottle of rum, and a shoulder parrot. Come back once you have those and we might talk.
                    -> Really?
                        King_Gull: I said "might." I'm gonna be looking to take a trip out of town soon. I might not mind getting back on the water.
                            <<set $fetchQuest=true>>
===
title: brawk
---
<<if $fetchQuest == true>>
<<jump brawk2>>
<<endif>>
Brawk: Hm...
    -> Look...
        <<die>>
===
title: brawk2
---
<<if $getParrot == true>>
<<jump getParrot>>
<<endif>>
Brawk:...
    -> I'm so sorry, but I have a question about shoulder parrots.
        <<die>>
===
title: getHat
---
Uncle_Al: Saw you talking to K.G. again. What're you two plotting?
    -> Now that I have a ship, he says he might mentor me, but I need a few things first.
        Uncle_Al: Ah, yeah, yeah. The "captain's starter pack," I suppose.
            -> Huh. I guess you could call it that. Actually, you have your old captain's hat from when you were sailing.
                Uncle_Al: Oh, for... fine. If K.G. thinks you have it in you, you can take the hat.
                    <<set $hat=true>>
                    <<hatGet Hero>>
===
title: getRum
---
Peach: Ah, you're back! Here, I wanted you to try a sip of this. Spiced peach rum!
Peach: You can't have the whole thing, of course, just a sample. Oh, this is going to be such a hit!
    -> Oh, is... that the bottle that's been here all day?
        Peach: Of course! Nothing like a bit of sun-warmed rum.
            -> I have bad news. I think I saw that kid spit in it before I came over.
                Peach: <i>Ugh!</i> I knew he was up to something! Here, get this away from me.
                    <<set $rum=true>>
            -> Peach, you know drinking isn't good for you. Maybe I should take that.
                Peach: Oh! Well, no one asked you, Piper.
                    -> I was just trying to help.
                    -> Your peaches taste like sand, anyway.
                        Convenient_Narrator: <i>Oh, Piper...</i>
                            <<die>>
===
title: onlyHat
---
<<if $hat == true && $rum == true>>
<<jump gotBoth>>
<<endif>>
King_Gull: Got the hat already, huh? Not too bad.
    -> It's good to have connections.
        King_Gull: Correct. Now go get that rum and shoulder parrot.
===
title: onlyRum
---
<<if $hat == true && $rum == true>>
<<jump gotBoth>>
<<endif>>
King_Gull: Got the rum already, huh? Not too bad.
    -> Being a little tricky comes in handy.
        King_Gull: Correct. Now go get that hat and shoulder parrot.

===
title: gotBoth
---
<<if $parrot == true>>
<<jump ready>>
<<endif>>
King_Gull: Well, well, this is promising. Used your wits to get as far as wits can get you.
    -> I don't think wits get you very far with Brawk.
        King_Gull: No, they do not. Time to teach you a third important pirate tactic.
            -> Wits are a pirate tactic? Hm... I guess that makes sense. I hear politcal deals happen sometimes.
                King_Gull: Yeah, ask your uncle how that went for him. Come on. Time to learn tactic number three.
                Convenient_Narrator: <i>King Gull subtly slips Piper a coin purse and gestures toward Brawk.</i>
                    King_Gull: Impress me a third time.
                        <<set $getParrot=true>>
===
title: getParrot
---
<<if $getParrot == true>>
Brawk: ...
    -> You don't know me well, but I have a proposition for you.
        <<die>>
    -> <i>Piper gently jingles the coin purse in her pocket.</i>
        Brawk: ...
        Brawk: You have everything else?
            -> <i>Piper nods and passes Brawk the money.</i>
                Brawk: You'll have a shoulder parrot when you're ready.
                Brawk: ...
                Brawk: You can go now.
                    <<set $parrot=true>>
<<endif>>
===
title: ready
---
King_Gull: Congratulations on surviving an encounter with Brawk. The man only cares about two things.
    -> Thanks for the tip.
        King_Gull: I'd be a pretty shoddy mentor if I didn't help you where you needed it. However...
        King_Gull: Pack your stuff, hug your uncle, and tell him your new apprenticeship is officially a go.
            -> Whoa, really?
                King_Gull: I was serious about getting out of town soon. Your first offical pirate adventure: Steal the Flying Frenchman.
                    -> That merchant ship moored just offscreen?
                        King_Gull: Oh yeah, you know what's up.
                            <<jump end>>
            -> Pack my stuff? The ship isn't ready.
                King_Gull: Look, cut the loss kid. Masts are expensive and there's a perfectly free ship moored just offscreen.
                    -> Wait, really?
                        King_Gull: I was serious about getting out of town soon. Your first offical pirate adventure: Steal the Flying Frenchman.
                            -> Alright, let's do it!
                            <<jump end>>
===
title: end
---
King_Gull: I'll gather up the parrot. You get yourself ready.
    -> Thinking of Brawk... What <i>is</i> a shoulder parrot?
        King_Gull: It's tradition for a pirate captain to sit on the shoulders of a parrot crewman when boarding a recently aquired vessel.
            -> Does that mean Brawk is coming with us?
                King_Gull: Of course. It'll be fine. Just watch out for that right hook.
                    Convenient_Narrator: <i>And so, Piper prepared herself for the bright and shiny crime-filled path laid out before her. Everyone cross their fingers.</i>
                    <<win>>
===
title: multiTrue
---
<<if $hat == true && $ship == true>>
    C_N: You'll get this.
<<else>>
    C_N: You get this instead.
<<endif>>
===
title: GameOver
---
// Convenient_Narrarator: 
Convenient_Narrarator: <i>Unfortunately for our intrepid hero, they died in one hit. </i>
Convenient_Narrarator: ...
Convenient_Narrarator: Wow, really?  All it took was one hit?
<<beginning>>
===
title: Victory
---
Convenient_Narrarator: <i>Piper eventually figured out what being a pirate was all about. Friends.</i>
Piper: yyyyyeeeeeAAAAAHHHHHHHH!
===